buildscript {
    repositories {
        mavenCentral()
        maven { url = "http://files.minecraftforge.net/maven" }
        maven { url = "https://oss.sonatype.org/content/repositories/snapshots/" }
        jcenter()
    }

    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:1.2-SNAPSHOT'
    }
}

apply plugin: 'forge'

dependencies {
    compile project(':worldedit-core')
    compile 'org.spongepowered:spongeapi:2.1-SNAPSHOT'
    testCompile group: 'org.mockito', name: 'mockito-core', version:'1.9.0-rc1'
}

repositories {
    maven { url ="https://repo.spongepowered.org/maven" }
}

minecraft {
    ext.forgeVersion = "11.14.0.1281"
    version = "1.8-$forgeVersion-1.8"

    replaceIn "com/sk89q/worldedit/forge/ForgeWorldEdit.java"
    replace "%VERSION%", project.version
}

project.archivesBaseName = "${project.archivesBaseName}-mc${minecraft.version}"

processResources {
    from (sourceSets.main.resources.srcDirs) {
        expand 'version': project.version,
                'mcVersion': project.minecraft.version,
                'forgeVersion': project.minecraft.forgeVersion,
                'internalVersion': project.internalVersion
        include 'mcmod.info'
    }

    from (sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}

jar {
    manifest {
        attributes("Class-Path": "truezip.jar WorldEdit/truezip.jar js.jar WorldEdit/js.jar",
                   "WorldEdit-Version": version)
    }
}

shadowJar {
    dependencies {
        include(dependency(':worldedit-core'))
    }
}

reobf.reobf(shadowJar) { spec ->
    spec.classpath = sourceSets.main.compileClasspath;
}

task deobfJar(type: Jar) {
    from sourceSets.main.output
    classifier = 'dev'
}

artifacts {
    archives deobfJar
}

build.dependsOn(shadowJar)
