buildscript {
    repositories {
        mavenCentral()
        maven { url = "http://files.minecraftforge.net/maven" }
        maven { url = "https://oss.sonatype.org/content/repositories/snapshots/" }
        jcenter()
    }

    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:1.2-SNAPSHOT'
        classpath 'com.github.jengelman.gradle.plugins:shadow:1.1.1'
    }
}

apply plugin: 'forge'
apply plugin: 'com.github.johnrengelman.shadow'

dependencies {
    compile project(':worldedit-core')
    testCompile group: 'org.mockito', name: 'mockito-core', version:'1.9.0-rc1'
}

minecraft {
    ext.forgeVersion = "10.13.1.1225"
    version = "1.7.10-$forgeVersion"

    replaceIn "com/sk89q/worldedit/forge/ForgeWorldEdit.java"
    replace "%VERSION%", project.version
}

processResources {
    from (sourceSets.main.resources.srcDirs) {
        expand 'version': project.version, 'mcVersion': project.minecraft.version, 'forgeVersion': project.minecraft.forgeVersion
        include 'mcmod.info'
    }

    from (sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}

shadowJar {
    classifier 'shadow'
    dependencies {
        include(dependency('com.sk89q:worldedit-core:jar:'))
        include(dependency('com.sk89q:jchronic:jar:'))
        include(dependency('com.thoughtworks.paranamer:paranamer:jar:'))
        include(dependency('com.sk89q.lib:jlibnoise:jar:'))
    }
    exclude 'GradleStart**'
    exclude '.cache'
}

reobf.reobf(shadowJar) { spec ->
    spec.classpath = sourceSets.main.compileClasspath;
}

task deleteOrig(type: Delete) {
    delete project.tasks.jar.archivePath.getPath()
}

task deleteShaded(type: Delete) {
    delete project.tasks.shadowJar.archivePath.getPath()
}

shadowJar.dependsOn(deleteShaded)
build.dependsOn(shadowJar)
