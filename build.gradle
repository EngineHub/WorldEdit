allprojects {
    apply plugin: 'java'
    apply plugin: 'maven'
    apply plugin: 'checkstyle'

    group = 'com.sk89q'
    version = '6.0.0-SNAPSHOT'

    sourceCompatibility = 1.6
    targetCompatibility = 1.6

    checkstyle.configFile = new File(rootProject.projectDir, "config/checkstyle/checkstyle.xml")

    repositories {
        mavenCentral()
        maven { url "http://repo.bukkit.org/content/groups/public" }
        maven { url "http://maven.sk89q.com/repo/" }
        maven { url "http://repo.maven.apache.org/maven2" }
    }

    if (JavaVersion.current().isJava8Compatible()) {
        // Java 8 turns on doclint which we fail
        tasks.withType(Javadoc) {
            options.addStringOption('Xdoclint:none', '-quiet')
        }
    }

    task sourcesJar(type: Jar, dependsOn:classes) {
        classifier = 'sources'
        from sourceSets.main.allSource
    }

    task javadocJar(type: Jar, dependsOn:javadoc) {
        classifier = 'javadoc'
        from javadoc.destinationDir
    }

    artifacts {
        archives jar
        archives sourcesJar
        archives javadocJar
    }

    build.dependsOn(checkstyleMain)
    build.dependsOn(checkstyleTest)
    build.dependsOn(sourcesJar)
    build.dependsOn(javadocJar)
}

dependencies {
    project(":worldedit-core")
    project(":worldedit-bukkit")
    project(":worldedit-forge")
}