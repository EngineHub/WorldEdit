apply plugin: 'eclipse'
apply plugin: 'idea'

dependencies {
    compile project(':worldedit-core')
    compile 'com.sk89q:dummypermscompat:1.7'
    compile 'org.sk89q.bukkit:bukkit-classloader-check:1.7.2-R0.3'
    compile 'org.bukkit:bukkit:1.7.2-R0.3'
    testCompile 'org.mockito:mockito-core:1.9.0-rc1'
}

processResources {
    from (sourceSets.main.resources.srcDirs) {
        expand 'internalVersion': project.internalVersion
        include 'plugin.yml'
    }

    from (sourceSets.main.resources.srcDirs) {
        exclude 'plugin.yml'
    }
}

jar {
    manifest {
        attributes("Class-Path": "truezip.jar WorldEdit/truezip.jar js.jar WorldEdit/js.jar",
                   "WorldEdit-Version": version)
    }
}

shadowJar {
    dependencies {
        include(dependency(':worldedit-core'))
        include(dependency('com.google.code.gson:gson:2.2.4'))
    }

    relocate('com.google.gson', 'com.sk89q.worldedit.internal.gson')
}

build.dependsOn(shadowJar)
