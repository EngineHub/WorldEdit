apply plugin: 'eclipse'
apply plugin: 'idea'
apply plugin: 'maven'

repositories {
    maven { url "https://hub.spigotmc.org/nexus/content/groups/public" }
    maven { url "https://repo.codemc.org/repository/maven-public" }
    maven { url 'https://papermc.io/repo/repository/maven-public/' }
}

configurations.all { Configuration it ->
    it.resolutionStrategy { ResolutionStrategy rs ->
        rs.force("com.google.guava:guava:21.0")
    }
}

dependencies {
    compile project(':worldedit-core')
    compile project(':worldedit-libs:bukkit')
    compile 'com.sk89q:dummypermscompat:1.10'
    compile 'org.bukkit:bukkit:1.13.2-R0.1-SNAPSHOT' // zzz
    compile 'com.destroystokyo.paper:paper-api:1.13.2-R0.1-SNAPSHOT'
    compile "io.papermc:paperlib:1.0.2"
    compile 'org.apache.logging.log4j:log4j-slf4j-impl:2.8.1'
    compile 'org.bstats:bstats-bukkit:1.4'
    testCompile 'org.mockito:mockito-core:1.9.0-rc1'
}

processResources {
    filesMatching('plugin.yml') {
        expand 'internalVersion': project.internalVersion
    }
    from (zipTree('src/main/resources/worldedit-adapters.jar').matching {
        exclude 'META-INF/'
    })
    exclude '**/worldedit-adapters.jar'
}

jar {
    manifest {
        attributes("Class-Path": "truezip.jar WorldEdit/truezip.jar js.jar WorldEdit/js.jar",
                   "WorldEdit-Version": version)
    }
}

shadowJar {
    dependencies {
        relocate "org.slf4j", "com.sk89q.worldedit.slf4j"
        relocate "org.apache.logging.slf4j", "com.sk89q.worldedit.log4jbridge"
        include(dependency(':worldedit-core'))
        include(dependency('org.slf4j:slf4j-api'))
        include(dependency("org.apache.logging.log4j:log4j-slf4j-impl"))
        relocate ("org.bstats", "com.sk89q.worldedit.bukkit.bstats") {
            include(dependency("org.bstats:bstats-bukkit:1.4"))
        }
        relocate ("io.papermc.lib", "com.sk89q.worldedit.bukkit.paperlib") {
            include(dependency("io.papermc:paperlib:1.0.2"))
        }
    }
}

build.dependsOn(shadowJar)
