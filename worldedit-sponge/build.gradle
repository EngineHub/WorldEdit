buildscript {
    repositories {
        mavenCentral()
        maven { url = "http://files.minecraftforge.net/maven" }
        maven { url = "http://repo.minecrell.net/releases" }
        maven { url = "https://oss.sonatype.org/content/repositories/snapshots/" }
        jcenter()
    }

    dependencies {
        classpath 'net.minecrell:VanillaGradle:2.0.3_1'
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.1-SNAPSHOT'
    }
}

plugins {
    id 'org.spongepowered.plugin' version '0.5.2'
}

apply plugin: 'net.minecrell.vanilla.server.library'

dependencies {
    compile project(':worldedit-core')
    compile 'org.spongepowered:spongeapi:4.1.0-SNAPSHOT'
    testCompile group: 'org.mockito', name: 'mockito-core', version:'1.9.0-rc1'
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

sponge {
    plugin {
        id = 'worldedit'
    }
}

minecraft {
    version = "1.8.9"
    mappings = "stable_22"
    runDir = 'run'
}

project.archivesBaseName = "${project.archivesBaseName}-mc${minecraft.version}"

jar {
    manifest {
        attributes("Class-Path": "truezip.jar WorldEdit/truezip.jar js.jar WorldEdit/js.jar",
                "WorldEdit-Version": version)
    }
}

shadowJar {
    dependencies {
        include(dependency(':worldedit-core'))
    }
}

reobf {
    shadowJar {
        mappingType = 'SEARGE'
    }
}

task deobfJar(type: Jar) {
    from sourceSets.main.output
    classifier = 'dev'
}

artifacts {
    archives deobfJar
}
